open import Data.Vec
open import Data.Product renaming (_,_ to ⟨_,_⟩)

module Logic.Adjoint   
  (Atom : Set)
  (Mode : Set)
  (mWeakenable : Mode → Set)
  (mContractable : Mode → Set)
  (mHarmless : Mode → Set)
  (_∙_⇒_ : Mode → Mode → Mode → Set)
  (_≥_ : Mode → Mode → Set) 
  where

  open import Logic.Core.Props Atom Mode public
  open import Logic.Core.Contexts 
    Atom 
    Mode  
    mWeakenable 
    mContractable 
    mHarmless 
    _∙_⇒_ 
    _≥_ 
    public

  data _⊢ⁱ_ : Context n → Prop × Mode → Set where

    id : update Δ ⟨ A , m ⟩ ⟨ A , k ⟩ Δ' → cWeakenable Δ' → mHarmless k
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ A , m ⟩

    cut : merge Δ₁ Δ₂ Δ₁₂ → merge Δ₂ Δ₃ Δ₂₃ → merge Δ₁₂ Δ₃ Δ
      → Δ₁ ≥ᶜ m → Δ₂ ≥ᶜ m → m ≥ k
      → cContractable Δ₂
      → Δ₁₂ ⊢ⁱ ⟨ A , m ⟩
      → (⟨ A , m ⟩ ∷ Δ₂₃) ⊢ⁱ ⟨ C , k ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ C , k ⟩

    ⊕R₁ : Δ ⊢ⁱ ⟨ A , m ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ A ⊕ B , m ⟩

    ⊕R₂ : Δ ⊢ⁱ ⟨ B , m ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ A ⊕ B , m ⟩

    ⊕L : mayConsume Δ ⟨ A ⊕ B , m ⟩ Δ'
      →  (⟨ A , m ⟩ ∷ Δ') ⊢ⁱ ⟨ C , k ⟩ → (⟨ B , m ⟩ ∷ Δ') ⊢ⁱ ⟨ C , k ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ C , k ⟩

    &R : Δ ⊢ⁱ ⟨ A , m ⟩ → Δ ⊢ⁱ ⟨ B , m ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ A & B , m ⟩

    &L₁ : mayConsume Δ ⟨ A & B , m ⟩ Δ'
      →  (⟨ A , m ⟩ ∷ Δ') ⊢ⁱ ⟨ C , k ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ C , k ⟩

    &L₂ : mayConsume Δ ⟨ A & B , m ⟩ Δ'
      →  (⟨ B , m ⟩ ∷ Δ') ⊢ⁱ ⟨ C , k ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ C , k ⟩

    ⊗R : merge Δ₁ Δ₂ Δ₁₂ → merge Δ₂ Δ₃ Δ₂₃ → merge Δ₁₂ Δ₃ Δ
      → cContractable Δ₂
      → Δ₁₂ ⊢ⁱ ⟨ A , m ⟩ → Δ₂₃ ⊢ⁱ ⟨ B , m ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ A ⊗ B , m ⟩

    ⊗L : mayConsume Δ ⟨ A ⊗ B , m ⟩ Δ'
      → (⟨ B , m ⟩ ∷ ⟨ A , m ⟩ ∷ Δ') ⊢ⁱ ⟨ C , k ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ C , k ⟩

    ⊸R : (⟨ A , m ⟩ ∷ Δ) ⊢ⁱ ⟨ B , m ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ A ⊸ B , m ⟩

    ⊸L : merge Δ₁ Δ₂ Δ₁₂ → merge Δ₂ Δ₃ Δ₂₃ → merge Δ₁₂ Δ₃ Δ
      → mayConsume Δ₁₂ ⟨ A ⊸ B , m ⟩ Δ₁₂'
      → mayConsume Δ₂₃ ⟨ A ⊸ B , m ⟩ Δ₂₃'
      → Δ₁ ≥ᶜ m → Δ₂ ≥ᶜ m → cContractable Δ₂
      → Δ₁₂ ⊢ⁱ ⟨ A , m ⟩
      → (⟨ B , m ⟩ ∷ Δ₂₃) ⊢ⁱ ⟨ C , k ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ C , k ⟩

    𝟙R : cWeakenable Δ
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ 𝟙 , m ⟩

    𝟙L : mayConsume Δ ⟨ 𝟙 , m ⟩ Δ' → Δ' ⊢ⁱ ⟨ C , k ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ C , k ⟩

    ↓R : merge Δ₁ Δ₂ Δ
      → Δ₁ ≥ᶜ m 
      → cWeakenable Δ₂
      → Δ₁ ⊢ⁱ ⟨ A , m ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ ↓[ m ][ k ] A , m ⟩

    ↓L : mayConsume Δ ⟨ ↓[ m ][ k ] A , m ⟩ Δ'
      → (⟨ A , m ⟩ ∷ Δ') ⊢ⁱ ⟨ C , l ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ C , l ⟩

    ↑R : Δ ⊢ⁱ ⟨ A , k ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ ↑[ m ][ k ] A , k ⟩

    ↑L : mayConsume Δ ⟨ ↑[ k ][ m ] A , k ⟩ Δ' → k ≥ l
      → (⟨ A , k ⟩ ∷ Δ') ⊢ⁱ ⟨ C , l ⟩
      ----------------------------------------------------------
      → Δ ⊢ⁱ ⟨ C , l ⟩
 